ext {
    springBootVersion = '1.3.1.RELEASE'
}

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "http://repo.spring.io/milestone" }
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }
    dependencies {
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '1.3.1.RELEASE'
        classpath 'org.ajoberstar:gradle-jacoco:0.3.0'
        classpath 'org.sonarqube.gradle:gradle-sonarqube-plugin:1.0'
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
    }
}

description = 'Cat-Boot'
group 'cc.catalysts.boot'
version '0.0.2-SNAPSHOT'

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'
apply plugin: 'build-dashboard'
apply plugin: 'maven-publish'

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                // TODO why aren't these settings taken from the root project?
                groupId 'cc.catalysts.boot'
                version '0.0.2-SNAPSHOT'

                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username System.getProperty('mavenUser')
                    password System.getProperty('mavenPassword')
                }
                url System.getProperty('mavenRepo')
            }
        }
    }
}

repositories {
    mavenCentral()
}